plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'checkstyle'
	id 'pmd'
	id 'jacoco'
}

group = 'com.dytes'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

checkstyle {
	toolVersion = '8.29'
	configFile = file('config/checkstyle.xml')
	maxWarnings = 0
	maxErrors = 0
}

jacoco {
	toolVersion = "0.8.4"
}

jacocoTestReport {
	reports {
		xml.enabled false
		html.enabled true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.0 // Set a proper coverage target
			}
		}
	}
}

dependencies {
	implementation 'com.google.guava:guava:28.0-jre'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.postgresql:postgresql:42.2.8'
	implementation 'org.flywaydb:flyway-core:5.+'

	compileOnly 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.hamcrest:hamcrest:2.2'
	implementation 'org.springframework.boot:spring-boot-starter-web'
}

test {
	useJUnitPlatform()
	exclude "com/dytes/todo/integration/**"
	testLogging {
		events "passed", "skipped", "failed"
	}
}

task integrationTest(type: org.gradle.api.tasks.testing.Test) {
	useJUnitPlatform()
	include "com/dytes/todo/integration/**"

	beforeTest { descriptor ->
		logger.lifecycle("Running test: " + descriptor)
	}

	onOutput { descriptor, event ->
		logger.lifecycle(event.message)
	}
}

bootRun {
	jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=0.0.0.0:5005"]
}

check {
	dependsOn 'integrationTest'
}
